/*
  This is a sample Custom Function written to be used in Workflow automation of Zoho Books.
	
  This custom function will associate the templates automatically based on branch.

  Create a connection by the name "zbooks" for Zoho Books service.

  Module               - estimate
  Workflow Type        - Event Based
  When an estimate is  - Created
  Filter the triggers  - (Optional)
  Actions              - Type: Custom function; Name: Choose the custom function that you have created
*/
estimateID = estimate.get("estimate_id");
estimatedate = estimate.get("date").toDate();
organizationID = organization.get("organization_id");
customerID = estimate.get("customer_id");
estimateNumber = estimate.get("estimate_number");
dc = organization.get("data_center_extension");
apiUrl = organization.get("api_root_endpoint");
estimateDetails=zoho.books.getRecordsByID("estimates", organizationID, estimateID,"zbooks").get("estimate");
bName = estimateDetails.get("branch_name");
url = apiUrl + "/settings/templates?template_group=estimate&organization_id=" + organizationID;
getTemplate = invokeurl
[
	url :url
	type :GET
	connection:"zbooks"
];
templates = getTemplate.get("templates");
for each  findTemp in templates
{
	tempName = findTemp.get("template_name");
	if(tempName.equalsIgnoreCase(bName))
	{
		NewTemplateID = findTemp.get("template_id");
	}
	
}
if(!NewTemplateID.isNull())
{
	url = apiUrl+"/estimates/" + estimateID + "/templates/" + NewTemplateID;
	params = Map();
	params.put("organization_id",organizationID);
	response = invokeurl
	[
		url :url
		type :PUT
		parameters:params
		connection:"zbooks"
	];
	info "Line->" + response;
}
else
{
	info "Cannot update an estimate";
}
