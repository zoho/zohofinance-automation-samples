/*

  This is a sample Custom Function written to be used in the Workflow automation of Zoho Books.
	
  This custom function will send the list of draft invoices to the cliq channel.
    
  Copy the unique name of the cliq channel that you have created and update the value of the variable "channelUniqueName".

  Create a connection by the name "zbooks" for the Zoho Books service and also create a connection by the name "zcliqq" for the Zoho Cliq service.
  
  Frquency                - Daily
  Expires After           - Never expires
  
*/

dateToday = today.toString("yyyy-MM-dd");
organizationID = organization.get("organization_id");
dc = organization.get("data_center_extension");
channelUniqueName = "testchanelt";

draftInvs = invokeurl
[
	url :"https://books.zoho" + dc + "/api/v3/invoices?created_date_start=" + dateToday + "&created_date_end=" + dateToday + "&status=draft&organization_id=" + organizationID
	type :GET
	connection:"zbooks"
];
info draftInvs;
draftInvs = draftInvs.get("invoices");
rows = List();
msgHead = today.toText("'*for ' MMM dd, yy'*'");
count = 0;
for each  find in draftInvs
{
	ro = Map();
	invoiceNum = find.get("invoice_number");
	customerName = find.get("customer_name");
	ro.put("Invoice Number",invoiceNum);
	ro.put("Customer Name",customerName);
	ro.put("Status","Draft");
	rows.add(ro);
	count = count + 1;
}
ro1 = Map();
ro1.put("Invoice Number","Total");
ro1.put("Customer Name",count);
rows.add(ro1);
table = {"type":"table","title":"Draft Invoices","data":{"headers":{"Invoice Number","Customer Name","Status"},"rows":rows}};
message = {"text":"Here's the list of draft invoices " + msgHead,"bot":{"name":"Notification"},"card":{"theme":"10"},"slides":{table}};
rep = invokeurl
[
	url :"https://cliq.zoho.com/api/v2/channelsbyname/" + channelUniqueName + "/message"
	type :POST
	parameters:message.toMap() + ""
	connection:"zcliqq"
];
info rep;
