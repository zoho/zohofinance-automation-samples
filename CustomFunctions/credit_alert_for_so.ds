/*
  This is a sample Custom Function written to be used in Workflow automation of Zoho Books.
	
  This Custom Function send an email to user if the credit limit is exceeded. 

  Please ensure to replace cf_checkbox with user level custom field api name in the script. 
  
  Create a connection by the name "zbooks" for Zoho Books service.

  Module               - salesorders
  Workflow Type        - Event Based
  When salesorder is   - Created
  Filter the triggers  - Status is Draft
  Actions              - Type: Custom function; Name: Choose the custom function that you have created
*/
salesorderID = salesorder.get("salesorder_id");
companyname = organization.get("name");
userFieldAPIName = "cf_checkbox";//Replace cf_checkbox with user level custom field api name.
organizationID = organization.get("organization_id");
salesorderNumber = salesorder.get("salesorder_number");
customerID = salesorder.get("customer_id");
apiUrl = organization.get("api_root_endpoint");
customerDetails = zoho.books.getRecordsByID("Contacts",organizationID,customerID,"zbooks").get("contact");
contactName = customerDetails.get("contact_name");
unUsedCredit = customerDetails.get("unused_credits_receivable_amount");
creditLimit = customerDetails.get("credit_limit");
outStanding = customerDetails.get("outstanding_receivable_amount").toDecimal() + salesorder.get("total").toDecimal();
outStandingAfterCredit = outStanding.toDecimal() - unUsedCredit.toDecimal();
if(outStandingAfterCredit >= creditLimit)
{
	emailList = List();
	getusers = invokeurl
	[
		url :apiUrl + "/users?" + userFieldAPIName + "=true&organization_id=" + organizationID
		type :GET
		connection:"zbooks"
	];
	users = getusers.get("users");
	for each  user in users
	{
		userEmail = user.get("email");
		emailList.add(userEmail);
	}
	if(emailList.size() > 0)
	{
		outstandingReceivables = customerDetails.get("outstanding_receivable_amount").toDecimal() - unUsedCredit.toDecimal();
		exceededAmount = outStandingAfterCredit - creditLimit.toDecimal();
		sub = "Subject:  Credit Limit alert for Sales Order - " + salesorderNumber + " from " + companyname;
		mailMsg = "<html><head><style>table {font-family: arial, sans-serif;border-collapse: collapse;width: 100%;}td, th {border: 1px solid #dddddd;textalign: left;padding: 8px;}tr:nth-child(even) {background-color: #dddddd;}</style></head><body>Hello,<br><br>A Sales Order has been created that exceeds the Credit limit of the customer - " + contactName + "<br><br><table><tr><td>Outstanding Receivables</td><td>" + outstandingReceivables + "</td></tr><tr><td>Sales Order Amount</td><td>" + salesorder.get("total") + "</td></tr><tr><td>Credit Limit</td><td>" + creditLimit + "</td></tr><tr><td>Total Outstanding Receivables</td><td>" + outStandingAfterCredit + "</td></tr><tr><td>Exceeded Amount</td><td>" + exceededAmount + "</td></tr></table><br><br>Thank you!</body></html>";
		sendmail
		[
			from :zoho.adminuserid
			to :emailList
			subject :sub
			message :mailMsg
		]
		info "The email has been sent to " + emailList;
	}
}
else
{
	info "Credit limit is less than outstanding";
}
