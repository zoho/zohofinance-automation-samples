/*
  This is a sample Custom Function written to be used in Workflow automation of Zoho Books.
	
  This Custom Function will do the Custom calculation in item table of invoices.

  Please ensure to replace[cf_field1,cf_field2,cf_rate] item level custom field api names in the script.
  
  Create a connection by the name "zbooks" for Zoho Books service.

  Module               - Invoices
  Workflow Type        - Event Based
  When Invoice is      - Created or Edited or Edited
  Whwn any field is updated.
  Filter the triggers  - (Optional)
  Actions              - Type: Custom function; Name: Choose the custom function that you have created
*/
invoiceID = invoice.get("invoice_id");
invoicedate = invoice.get("date").toDate();
organizationID = organization.get("organization_id");
customerID = invoice.get("customer_id");
field1Placeholder="cf_field1";//Replace cf_field1 with API name of the item custom field 
field2Placeholder="cf_field2";//Replace cf_field2 with API name of the item custom field 
ratePlaceholder="cf_rate";//Replace cf_rate with API name of the item custom field 
invoiceDetails = zoho.books.getRecordsByID("Invoices",organizationID,invoiceID,"zbooks").get("invoice");
lineItems = invoiceDetails.get("line_items").toList();
iList = List();
for each  findItem in lineItems
{
	iMap = Map();
	itemrateCalculation = 1;
	field1Value = 0;
	uRate = 0;
	field2Value=0;
	rate = findItem.get("rate").toDecimal();
	quantity = findItem.get("quantity").toDecimal();
	itemCFList = List();
	itemCF = findItem.get("item_custom_fields").toList();
	for each  findItemCF in itemCF
	{
		itemLab = findItemCF.get("label");
		itemVal = findItemCF.get("value");
		placeHolder = findItemCF.get("placeholder");
		if(placeHolder == field1Placeholder)
		{
			
			field1Value = findItemCF.get("value").toDecimal();
			
		}
		if(placeHolder == field2Placeholder)
		{
			
			field2Value = findItemCF.get("value").toDecimal();
		}
		else if(placeHolder == ratePlaceholder)
		{
			
			uRate = findItemCF.get("value").toDecimal();
		}
		itemMap = Map();
		itemMap.put("api_name",placeHolder);
		itemMap.put("value",itemVal);
		itemCFList.add(itemMap);
	}
	
	itemMap = Map();
	itemMap.put("api_name",ratePlaceholder);
	if(!uRate.isNull() && uRate != 0)
	{
		itemMap.put("value",uRate);
	}
	else
	{
		itemMap.put("value",rate);
	}
	itemCFList.add(itemMap);
	if(!field1Value.isNull() && field1Value != 0&&!field2Value.isNull()&& field2Value!=0 )
	{
		if(!uRate.isNull() && uRate != 0)
		{
			itemrateCalculation = uRate * field1Value*field2Value;
		}
		else
		{
			itemrateCalculation = rate * field1Value*field2Value;
		}
	}
	else
	{
		if(!uRate.isNull() && uRate != 0)
		{
			itemrateCalculation = 1;
			itemrateCalculation = itemrateCalculation * uRate;
		}
		else
		{
			itemrateCalculation = 1;
			itemrateCalculation = itemrateCalculation * rate;
		}
	}

	lineItemMap=Map();
	lineItemMap.put("item_custom_fields",itemCFList);
	lineItemMap.put("line_item_id",findItem.get("line_item_id"));
	lineItemMap.put("rate",itemrateCalculation);
	iList.add(lineItemMap);
}
	json = Map();
	json.put("line_items",iList);
	json.put("customer_id",customerID);
	json.put("reason","Custom Calculation");
	updateInvoice = zoho.books.updateRecord("Invoices",organizationID,invoiceID,json,"zbooks");
	info updateInvoice.get("message");
